apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

group = 'com.github.MatrixDev'

dependencies {
    implementation project(':RoomigrantLib')

    // Kotlin runtime
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Kotlin code generation
    implementation('com.squareup:kotlinpoet:1.6.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
    }

    // Room
    implementation 'androidx.room:room-runtime:2.2.5'

    // Json parsing
    implementation 'com.google.code.gson:gson:2.8.6'

    // Auto META-INF processor info generator
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'com.google.auto.service:auto-service:1.0-rc6'

    // Incremental processor support
    compileOnly 'net.ltgt.gradle.incap:incap:0.2'
    kapt 'net.ltgt.gradle.incap:incap-processor:0.2'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'dev.matrix.roomigrant'
            artifactId 'roomigrant-compiler'
            version roomigrant_version
            artifact("$buildDir/libs/RoomigrantCompiler.jar")
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/MatrixDev/Roomigrant"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
